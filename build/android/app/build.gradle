apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
	compileSdkVersion 30
	buildToolsVersion '30.0.3'
	ndkVersion '22.1.7171670'
	defaultConfig {
		applicationId 'com.multicraft.game'
		minSdkVersion 19
		targetSdkVersion 30
		versionCode 170
		versionName "1.16.0"
	}

	// load properties
	Properties props = new Properties()
	def propfile = file('../local.properties')
	if (propfile.exists())
		props.load(new FileInputStream(propfile))

	if (props.getProperty('keystore') != null) {
		signingConfigs {
			release {
				storeFile file(props['keystore'])
				storePassword props['keystore.password']
				keyAlias props['key']
				keyPassword props['key.password']
			}
		}

		buildTypes {
			release {
				shrinkResources true
				minifyEnabled false
				signingConfig signingConfigs.release
			}
			debug {
				debuggable true
				minifyEnabled false
			}
		}
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}

	// for multiple APKs
	splits {
		abi {
			enable true
			reset()
			include 'armeabi-v7a', 'arm64-v8a'
		}
	}
}

dependencies {
	/* MultiCraft Native */
	implementation project(':native')

	/* Third-party libraries */
	implementation 'androidx.appcompat:appcompat:1.3.0'
	implementation 'androidx.appcompat:appcompat-resources:1.3.0'
	implementation 'com.google.android.material:material:1.4.0'
	implementation 'io.reactivex.rxjava3:rxjava:3.0.13'
	implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
	implementation 'net.lingala.zip4j:zip4j:2.9.0'
}
